
!symbian {
    INCLUDEPATH += $${MOBILITY_INCLUDE}
} else {
    load(data_caging_paths)
    contains(MOBILITY, contacts|versit) {
        INCLUDEPATH+=$$APP_LAYER_SYSTEMINCLUDE
    }

    contains(MOBILITY,bearer|location|publishsubscribe|systeminfo|multimedia|messaging|serviceframework|sensors) {
        INCLUDEPATH+=$$MW_LAYER_SYSTEMINCLUDE
    }
}
mac: LIBS += -F$${MOBILITY_LIB}
LIBS+= -L$${MOBILITY_LIB}
QMAKE_RPATHDIR+=$${MOBILITY_LIB}

contains(MOBILITY, bearer) {
    DEFINES += QT_MOBILITY_BEARER
    qtAddLibrary(QtBearer)
    !equals(QT_MAJOR_VERSION,4)|!equals(QT_MINOR_VERSION, 6) {
        message(Qt Mobility Bearer API is deprecated when using Qt 4.7 or higher.)
        message(Please use source compatible replacement Bearer API in QtNetwork.)
    }
}

contains(MOBILITY, publishsubscribe) {
    qtAddLibrary(QtPublishSubscribe)
}

contains(MOBILITY, location) {
    qtAddLibrary(QtLocation)
}

contains(MOBILITY, serviceframework) {
    qtAddLibrary(QtServiceFramework)
}

contains(MOBILITY, systeminfo) {
    #QSystemInfo header requires QNetworkInterface
    QT += network
    qtAddLibrary(QtSystemInfo)
}

contains(MOBILITY, multimedia) {
    QT += network
    contains(QT_CONFIG, opengl): QT += opengl
    mac:LIBS += -framework QtMultimedia
    else:win32:contains(QT_CONFIG, debug):LIBS += -lQtMultimediad
    else:LIBS += -lQtMultimedia
}

contains(MOBILITY, contacts) {
    qtAddLibrary(QtContacts)
}

contains(MOBILITY, messaging) {
    qtAddLibrary(QtMessaging)
}

contains(MOBILITY, versit) {
    qtAddLibrary(QtVersit)
}

contains(MOBILITY, sensors) {
    qtAddLibrary(QtSensors)
}

# Add dependency to QtMobility package to all projects besides QtMobility package itself.
# Mobility libs have UID3 0x2002AC89
# self-signed Mobility libs have UID3 0xE002AC89
symbian:contains(CONFIG, mobility):!contains(TARGET.UID3, 0x2002AC89):!contains(TARGET.UID3, 0xE002AC89) {
    mobility_default_deployment.pkg_prerules += \
        "; Default dependency to QtMobility libraries" \
        "(0x2002AC89), 1, 0, 1, {\"QtMobility\"}"
    DEPLOYMENT += mobility_default_deployment
}

